plugins {
    id 'org.springframework.boot' version '2.1.0.RELEASE'
    id "com.github.spotbugs" version "1.6.4"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = projectJavaVersion
targetCompatibility = projectJavaVersion

version = '0.1-SNAPSHOT'

sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/groovy')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
//    maven { url "https://repo.spring.io/snapshot" }
//    maven { url "https://repo.spring.io/milestone" }
}

dependencyManagement {
    imports {
    }
}

dependencies {
    // Spring
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    // Spring dev tools
    compile('org.springframework.boot:spring-boot-devtools')
    // Lombok
    compile('org.projectlombok:lombok')

    runtime('javax.xml.bind:jaxb-api:2.3.0')

    // Testing
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
    // Groovy version for test compilation
    testCompile 'org.codehaus.groovy:groovy-all:2.4.13'

    // PostgreSQL
    runtime('org.postgresql:postgresql')
    // H2
    runtime('com.h2database:h2')
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
}

checkstyle {
    sourceSets = [sourceSets.main]

    ignoreFailures = false
}

spotbugs {
    effort = 'max'
    reportLevel = 'high'

    sourceSets = [sourceSets.main]

//    includeFilter = file("findbugs-exclude.xml")

    ignoreFailures = false
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    sourceSets = [sourceSets.main]

    ignoreFailures = false
}

jacoco {
}

task wrapper(type: Wrapper) {
    gradleVersion = '5.5'
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
check.dependsOn jacocoTestReport
